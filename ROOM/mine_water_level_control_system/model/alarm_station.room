RoomModel alarm_station {
	import room.basic.types.* from "../../org.eclipse.etrice.modellib.java/model/Types.room"
	import devices.* 		  from "devices.room"
	import logger.* 	      from "logger.room"
	
	DataClass alarm_controller_idata_t {
		Attribute alarm 			         : device_t ref
		Attribute number_of_alarm_activators : int32
	}
	
	DataClass alarm_controller_t {
		Attribute alarm 	  				 : device_t ref
		Attribute alarm_level 				 : int32
		Attribute number_of_alarm_activators : int32

		Operation initialize (
			logger 	   : logger_t, 
			actor_name : string,
			data 	   : alarm_controller_idata_t
		) : void '''
			logger.info (
				actor_name,
				"Initializing alarm controller"
			);

			this.alarm_level 				= 0;
			this.alarm 		 				= data.alarm;
			this.number_of_alarm_activators = data.number_of_alarm_activators;
			
			this.alarm.set_state (
				logger,
				actor_name,
				device_state_t.OFF
			);
		'''
				
		Operation turn_on (
			logger 	   : logger_t, 
			actor_name : string
		) : void '''
			synchronized ( this ) { 
				if ( this.alarm_level < this.number_of_alarm_activators ) {
					this.alarm_level++;
				}
			
				if ( this.alarm_level == 1 ) {
					logger.info (
						actor_name,
						"Initial turn on message recevied, turning on alarm"
					);

					this.alarm.set_state (
						logger,
						actor_name,
						device_state_t.ON
					);
				}
			}
		'''

		Operation turn_off ( 
			logger     : logger_t, 
			actor_name : string
		) : void '''
			if ( this.alarm_level > 0 ) {
				this.alarm_level--;
			}
			
			if ( this.alarm_level == 0 ) {
				logger.info (
					actor_name,
					"Last turn off message recevied, turning off alarm"
				);
				
				this.alarm.set_state (
					logger,
					actor_name,
					device_state_t.OFF
				);
			}
		'''
	}
}
