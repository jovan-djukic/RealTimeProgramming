RoomModel test {
	import room.basic.types.* from "../../../org.eclipse.etrice.modellib.java/model/Types.room"
	import room.basic.service.timing.* from "../../../org.eclipse.etrice.modellib.java/model/TimingService.room"
	import devices.* from "../../model/devices.room"
	import environment_monitoring_station.* from "../../model/environment_monitoring_station.room"

	LogicalSystem log_sys {
		SubSystemRef sub_system_ref: sub_sys_class 
	}

	SubSystemClass sub_sys_class {
		ActorRef top_actor: top_actor_t 
		ActorRef timing_service: ATimingService
		LayerConnection ref top_actor satisfied_by timing_service.timer
		LogicalThread thread
	}

	ActorClass top_actor_t {
		Structure {
			usercode1 '''
				class Constants {
					public static final int HIGH_WATER_LEVEL_DETECTOR_CONTROLLER_PERIOD_IN_MS = 500;
					public static final int TEST_PERIOD_IN_MS = 750;
					public static final int WATER_LEVEL_DETECTOR_IVALUE = 0;
				}
			'''		
			
			Attribute water_level_detector : water_level_detector_t
			ActorRef high_water_level_detector_controller : high_water_level_detector_controller_t
			
			conjugated Port high_water_level_detector_iport : environmonet_statition_actor_base_iprotocol_t
			conjugated Port pump_switch_port : switch_protocol_t

			SAP timer_port : PTimer 
			
			Binding high_water_level_detector_iport and high_water_level_detector_controller.iport
			Binding pump_switch_port and high_water_level_detector_controller.pump_switch_port
		}
		Behavior {
			StateMachine {
				State testing { }
				
				Transition start : initial -> testing {
					action '''
						this.water_level_detector.value = Constants.WATER_LEVEL_DETECTOR_IVALUE;
						
						this.high_water_level_detector_iport.initialize ( 
							new water_level_detector_controller_base_idata_t ( 
								Constants.HIGH_WATER_LEVEL_DETECTOR_CONTROLLER_PERIOD_IN_MS,
								this.water_level_detector
							)
						);
						
						this.timer_port.startTimeout ( Constants.TEST_PERIOD_IN_MS );
					'''
				}
				
				
				Transition turn_off_message_received : testing -> testing {
					triggers { 
						< turn_off : pump_switch_port >
					}
					action '''
						System.err.println ( "TURN OFF MESSAGE RECEIVED UNEXPECTEDLY " );
					'''
				}

				Transition turn_on_message_received : testing -> testing {
					triggers { 
						< turn_on : pump_switch_port >
					}
					action '''
						if ( this.water_level_detector.value != 1 ) {
							System.err.println ( "TURN ON MESSAGE RECEIVED UNEXPECTEDLY " );
						} else {
							System.err.println ( "TURN ON MESSAGE RECEIVED EXPECTEDLY " );
						}
					'''
				}
				
				Transition timeout_message_received : testing -> testing {
					triggers {
						< timeout : timer_port > 
					}
					action '''
						if ( Math.random ( ) > 0.5 ) {
							this.water_level_detector.value = 1 - this.water_level_detector.value;	
						}
						this.timer_port.startTimeout ( Constants.TEST_PERIOD_IN_MS );
					'''
				}
			}
		}
	}
}