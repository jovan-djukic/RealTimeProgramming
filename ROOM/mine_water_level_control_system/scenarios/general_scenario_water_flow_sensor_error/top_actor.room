RoomModel top_actor {
	import room.basic.service.timing.* from "../../../org.eclipse.etrice.modellib.java/model/TimingService.room"
	import room.basic.types.* from "../../../org.eclipse.etrice.modellib.java/model/Types.room"
	import base_actor.* from "../base_actor.room"

	ActorClass top_actor_t extends base_actor_t {
		Structure {
			Attribute number_of_activations : int32
		}
		Behavior {
			StateMachine {
				State step1 { }
				State step2 { }
				State step3 { }
				State step4 { }
				State step5 { }
				State step6 { }
				State step7 { }
				State step8 { }
				State step9 { }
				State step10 { }
				State step11 { }
				State step12 { }

				RefinedTransition start { 
					action '''
						super.timer_access_point.startTimeout (
							CONSTANTS.SCENARIO.CHECK_PERIOD
						);
					'''
				}

				Transition transition0 : step0 -> step1 {
					triggers {
						< timeout : timer_access_point >
					}
					action '''
						System.out.println ( "Turning on pump by using user port" );

						super.mine_water_level_control_system_user_port.turn_on ( );

						super.timer_access_point.startTimeout (
							CONSTANTS.SCENARIO.CHECK_PERIOD
						);
					'''
				}

					
				Transition stall1 : step1 -> step1 {
					triggers {
						< timeout : timer_access_point guard '''
							super.pump.state != device_state_t.ON
						'''>
					}
					action '''
						super.timer_access_point.startTimeout (
							CONSTANTS.SCENARIO.CHECK_PERIOD
						);
					'''
				}
				Transition transition1 : step1 -> step2 {
					triggers {
						< timeout : timer_access_point guard '''
							super.pump.state == device_state_t.ON
						'''>
					}
					action '''
						System.out.println ( "Pump activated" );
					'''
				}
					
				Transition transition2 : step2 -> step3 {
					triggers {
						< checking : mine_water_level_control_system_test_port >
					}
					action '''
						System.out.println ( "Water flow sensor controller detected state change" );
						
						this.number_of_activations = 0;
					'''
				}

				Transition stall3 : step3 -> step3 {
					triggers {
						< checking : mine_water_level_control_system_test_port guard '''
							this.number_of_activations != ( CONSTANTS.WATER_FLOW_SENSOR_CONTROLLER.NUMBER_OF_ACTIVATIONS - 1 )	
						''' >
					}
					action '''
						this.number_of_activations++;
						System.out.println ( "Water flow sensor activated, " + this.number_of_activations + " / " + CONSTANTS.WATER_FLOW_SENSOR_CONTROLLER.NUMBER_OF_ACTIVATIONS );
					'''
				}	
					
				Transition transition3 : step3 -> step4 {
					triggers {
						< checking : mine_water_level_control_system_test_port guard '''
							this.number_of_activations == ( CONSTANTS.WATER_FLOW_SENSOR_CONTROLLER.NUMBER_OF_ACTIVATIONS - 1 )	
						''' >
					}
					action '''
						System.out.println ( "One more try left before activation of alarm" );
					'''
				}

				Transition transition4 : step4 -> step5 {
					triggers {
						< alarm : mine_water_level_control_system_test_port >
					}
					action '''
						System.out.println ( "Water flow sensor controller activated alarm" );
						
						super.timer_access_point.startTimeout (
							CONSTANTS.SCENARIO.CHECK_PERIOD
						);
					'''
				}

				Transition stall5 : step5 -> step5 {
					triggers {
						< timeout : timer_access_point guard '''
							super.alarm.state != device_state_t.ON
						'''>
					}
					action '''
						super.timer_access_point.startTimeout (
							CONSTANTS.SCENARIO.CHECK_PERIOD
						);
					'''
				}	
					
				Transition transition5 : step5 -> step6 {
					triggers {
						< timeout : timer_access_point guard '''
							super.alarm.state == device_state_t.ON	
						'''>
					}
					action '''
						System.out.println ( "Alarm activated" );

						System.out.println ( "Pausing for " + CONSTANTS.SCENARIO.SLEEP_TIME_IN_S + " seconds" );
						
						try {
							java.lang.Thread.sleep ( CONSTANTS.SCENARIO.SLEEP_TIME_IN_S * 1000 );
						} catch ( InterruptedException e ) { }
						
						System.out.println ( "Setting water flow sensor" );
						super.water_flow_sensor.value = 1;
					'''
				}

				Transition transition6 : step6 -> step7 {
					triggers {
						< checking : mine_water_level_control_system_test_port >
					}
					action '''
						System.out.println ( "Water flow sensor controller detected state change" );
					'''
				}


				Transition transition7 : step7 -> step8 {
					triggers {
						< activated : mine_water_level_control_system_test_port>
					}
					action '''
						System.out.println ( "Pump state confirmed" );
						
						System.out.println ( "Pausing for " + CONSTANTS.SCENARIO.SLEEP_TIME_IN_S + " seconds" );
						
						try {
							java.lang.Thread.sleep ( CONSTANTS.SCENARIO.SLEEP_TIME_IN_S * 1000 );
						} catch ( InterruptedException e ) { }

						System.out.println ( "Turning off pump using user port" );

						super.mine_water_level_control_system_user_port.turn_off ( );

						super.timer_access_point.startTimeout (
							CONSTANTS.SCENARIO.CHECK_PERIOD
						);
					'''
				}

				Transition stall8 : step8 -> step8 {
					triggers {
						< timeout : timer_access_point guard '''
							super.pump.state != device_state_t.OFF
						'''>
					}
					action '''
						super.timer_access_point.startTimeout (
							CONSTANTS.SCENARIO.CHECK_PERIOD
						);
					'''
				}	
					
				Transition transition8 : step8 -> step9 {
					triggers {
						< timeout : timer_access_point guard '''
							super.pump.state == device_state_t.OFF
						'''>
					}
					action '''
						System.out.println ( "Pump deactivated" );
					'''
				}

				Transition transition9 : step9 -> step10 {
					triggers {
						< checking : mine_water_level_control_system_test_port >
					}
					action '''
						System.out.println ( "Water flow sensor controller detected state change" );
						
						this.number_of_activations = 0;
					'''
				}

				Transition stall10 : step10 -> step10 {
					triggers {
						< checking : mine_water_level_control_system_test_port guard '''
							this.number_of_activations != ( CONSTANTS.WATER_FLOW_SENSOR_CONTROLLER.NUMBER_OF_ACTIVATIONS - 1 )
						''' >
					}
					action '''
						this.number_of_activations++;
						System.out.println ( "Water flow sensor activated, " + this.number_of_activations + " / " + CONSTANTS.WATER_FLOW_SENSOR_CONTROLLER.NUMBER_OF_ACTIVATIONS );
					'''
				}	
					
				Transition transition10 : step10 -> step11 {
					triggers {
						< checking : mine_water_level_control_system_test_port guard '''
							this.number_of_activations == ( CONSTANTS.WATER_FLOW_SENSOR_CONTROLLER.NUMBER_OF_ACTIVATIONS - 1 )
						''' >
					}
					action '''
						System.out.println ( "Unsetting water flow sensor" );
						
						super.water_flow_sensor.value = 0;
					'''
				}

				Transition transition11 : step11 -> step12 {
					triggers {
						< activated : mine_water_level_control_system_test_port >
					}
					action '''
						System.out.println ( "Pump state confirmed" );
						
						super.stop_scenario ( );
					'''
				}
			}
		}
	}
}
